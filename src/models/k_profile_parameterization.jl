module KPP

using OceanTurb

import OceanTurb: Constants

using Base: @propagate_inbounds

const nsol = 4
@solution U V T S

"""
    Parameters(; kwargs...)

Construct KPP parameters.
"""
@Base.kwdef struct Parameters{T<:AbstractFloat} <: AbstractParameters
    CSL   :: T  = 0.1   # Surface layer fraction
    CÏ„    :: T  = 0.4   # Von Karman constant
    CNL   :: T  = 6.33  # Non-local flux proportionality constant

    Cstab :: T  = 2.0   # Stable buoyancy flux parameter for wind-driven turbulence
    Cunst :: T  = 6.4   # Unstable buoyancy flux parameter for wind-driven turbulence

       Cn :: T  = 1.0   # Exponent for effect of stable buoyancy forcing on wind mixing
    CmÏ„_U :: T  = 0.25  # Exponent for effect of unstable buoyancy forcing on wind mixing of U
    CmÏ„_T :: T  = 0.5   # Exponent for effect of unstable buoyancy forcing on wind mixing of T
    Cmb_U :: T  = 1/3   # Exponent for the effect of wind on convective mixing of U
    Cmb_T :: T  = 1/3   # Exponent for effect of wind on convective mixing of T

    Cd_U  :: T  = 0.5   # Wind mixing regime threshold for momentum
    Cd_T  :: T  = 2.5   # Wind mixing regime threshold for tracers

    Cb_U  :: T  = 0.599 # Buoyancy flux parameter for convective turbulence
    Cb_T  :: T  = 1.36  # Buoyancy flux parameter for convective turbulence
    CÏ„b_U :: T  = (CÏ„ / Cb_U)^(1/Cmb_U) * (1 + Cunst*Cd_U)^(CmÏ„_U/Cmb_U) - Cd_U  # Wind stress parameter for convective turbulence
    CÏ„b_T :: T  = (CÏ„ / Cb_T)^(1/Cmb_T) * (1 + Cunst*Cd_T)^(CmÏ„_T/Cmb_T) - Cd_T  # Wind stress parameter for convective turbulence

    CRi   :: T  = 0.3   # Critical bulk Richardson number
    CKE   :: T  = 4.32  # Unresolved turbulence parameter
    CKEâ‚€  :: T  = 1e-11 # Minimum unresolved turbulence kinetic energy

    KUâ‚€   :: T  = 1e-6  # Interior viscosity for velocity
    KTâ‚€   :: T  = 1e-7  # Interior diffusivity for temperature
    KSâ‚€   :: T  = 1e-9  # Interior diffusivity for salinity
end

# Shape functions.
# 'd' is a non-dimensional depth coordinate.
default_NL_shape(d) = ifelse(0<d<1, d*(1-d)^2, -zero(d))
const default_K_shape = default_NL_shape

mutable struct State{T} <: FieldVector{6, T}
    Fu :: T
    Fv :: T
    FÎ¸ :: T
    Fs :: T
    Fb :: T
    h  :: T
end

State(T=Float64) = State{T}(0, 0, 0, 0, 0, 0)


mutable struct Model{S, G, T, U, B} <: AbstractModel{S, G, T}
    clock       :: Clock{T}
    grid        :: G
    timestepper :: S
    solution    :: U
    bcs         :: B
    parameters  :: Parameters{T}
    constants   :: Constants{T}
    state       :: State{T}
end


function Model(; N=10, L=1.0,
            grid = UniformGrid(N, L),
       constants = Constants(),
      parameters = Parameters(),
         stepper = :ForwardEuler
              )

     K = (U=KU, V=KV, T=KT, S=KS)
     R = (U=RU, V=RV, T=RT, S=RS)
    eq = Equation(R=R, K=K, update=update_state!)

    bcs = (
        U = DefaultBoundaryConditions(eltype(grid)),
        V = DefaultBoundaryConditions(eltype(grid)),
        T = DefaultBoundaryConditions(eltype(grid)),
        S = DefaultBoundaryConditions(eltype(grid))
    )

    solution = Solution(
        CellField(grid),
        CellField(grid),
        CellField(grid),
        CellField(grid)
    )

    lhs = OceanTurb.build_lhs(solution)
    timestepper = Timestepper(stepper, eq, solution, lhs)
    clock = Clock()
    state = State()

    return Model(clock, grid, timestepper, solution, bcs, parameters, constants, state)
end

# Note: we use 'm' to refer to 'model' in function definitions below.

@inline Fb(g, Î±, Î², FÎ¸, Fs) = g * (Î±*FÎ¸ - Î²*Fs)

@propagate_inbounds d(m, i) = ifelse(m.state.h>0, -m.grid.zf[i]/m.state.h, -zero(m.state.h))

"Return the buoyancy gradient at face point i."
@propagate_inbounds âˆ‚Bâˆ‚z(T, S, g, Î±, Î², i) = g * (Î±*âˆ‚z(T, i) - Î²*âˆ‚z(S, i))

"""
    update_state!(model)

Update the top flux conditions and mixing depth for `model`
and store in `model.state`.
"""
function update_state!(m)
    m.state.Fu = getbc(m, m.bcs.U.top)
    m.state.Fv = getbc(m, m.bcs.V.top)
    m.state.FÎ¸ = getbc(m, m.bcs.T.top)
    m.state.Fs = getbc(m, m.bcs.S.top)
    m.state.Fb = Fb(m.constants.g, m.constants.Î±, m.constants.Î², m.state.FÎ¸, m.state.Fs)
    m.state.h  = mixing_depth(m)
    return nothing
end

#
# Diagnosis of mixing depth "h"
#

"Returns the surface_layer_average for mixing depth h = -zf[i]."
@propagate_inbounds function surface_layer_average(c, CSL::T, i) where T
    if i > c.grid.N # Return surface value
        return onface(c, c.grid.N+1)
    else
        iÎµ = length(c)+1 - CSL*(length(c)+1 - i) # (fractional) face "index" of the surface layer
        face = ceil(Int, iÎµ)  # next cell face above the fractional depth
        frac = face - iÎµ # fraction of lowermost cell in the surface layer.
        surface_layer_integral = zero(T)

        # Contribution of fractional cell to total integral
        if face > 1
            surface_layer_integral += frac * Î”f(c, face-1) * c[face-1]
        else
            face = 1
        end

        # Add cells above face, if there are any.
        for j = face:length(c)
            surface_layer_integral += Î”f(c, j) * c[j]
        end

        h = -c.grid.zf[i] # depth

        return surface_layer_integral / (CSL*h)
    end
end

"""
Return Î”c(háµ¢), the difference between the surface-layer average of c and its value at depth háµ¢, where
i is a face index.
"""
@propagate_inbounds Î”(c, CSL, i) = surface_layer_average(c, CSL, i) - onface(c, i)

"Returns the parameterization for unresolved KE at face point i."
@inline function unresolved_kinetic_energy(h, Bz, Fb, CKE, CKEâ‚€, g, Î±, Î²)
    return CKE * h^(4/3) * sqrt(max(0, Bz)) * max(0, Fb)^(1/3) + CKEâ‚€
end

"""
    bulk_richardson_number(model, i)

Returns the bulk Richardson number of `model` at face `i`.
"""
@propagate_inbounds function bulk_richardson_number(
            U, V, T, S, Fb::TT, CKE::TT, CKEâ‚€::TT, CSL::TT,
            g::TT, Î±::TT, Î²::TT, i) where TT

    h = -U.grid.zf[i]
    # (h - hÎµ) * Î”B
    hâºÎ”B = h * (one(TT) - CSL/2) * g * (Î±*Î”(T, CSL, i) - Î²*Î”(S, CSL, i))

    KE = (Î”(U, CSL, i)^2 + Î”(V, CSL, i)^2
              + unresolved_kinetic_energy(h, âˆ‚Bâˆ‚z(T, S, g, Î±, Î², i), Fb, CKE, CKEâ‚€, g, Î±, Î²))

    if KE == 0 && hâºÎ”B == 0 # Alistar Adcroft's theorem
        return -zero(TT)
    else
        return hâºÎ”B / KE
    end
end

@propagate_inbounds bulk_richardson_number(m, i) = bulk_richardson_number(
    m.solution.U, m.solution.V, m.solution.T, m.solution.S,
    m.state.Fb, m.parameters.CKE, m.parameters.CKEâ‚€, m.parameters.CSL, m.constants.g,
    m.constants.Î±, m.constants.Î², i)

"""
    mixing_depth(model)

Calculate the mixing depth 'h' for `model`.
"""
function mixing_depth(m)
    ihâ‚ = m.grid.N + 1 # start at top.
    @inbounds Riâ‚ = bulk_richardson_number(m, ihâ‚) # should be 0.

    # Descend through grid until Ri rises above critical value
    while ihâ‚ > 1 && Riâ‚ < m.parameters.CRi
        ihâ‚ -= 1 # descend
        @inbounds Riâ‚ = bulk_richardson_number(m, ihâ‚)
    end

    # Edge cases:
    # 1. Mixing depth is at the top of the domain (z=0):
    if ihâ‚ == m.grid.N + 1
        @inbounds zâ˜… = m.grid.zf[ihâ‚]

    # 2. Mixing depth is whole domain because Ri is always less than CRi:
    elseif ihâ‚ == 1 && Riâ‚ < m.parameters.CRi
        @inbounds zâ˜… = m.grid.zf[ihâ‚]

    # 3. Ri is infinite somewhere inside the domain.
    elseif !isfinite(Riâ‚)
        @inbounds zâ˜… = m.grid.zc[ihâ‚]

    # Main case: mixing depth is in the interior.
    else # Riâ‚ > CRi
        Î”Ri = bulk_richardson_number(m, ihâ‚+1) - Riâ‚ # <0 linearly interpolate to find h.
        # x = xâ‚€ + Î”x * (y-yâ‚€) / Î”y
        @inbounds zâ˜… = m.grid.zf[ihâ‚] + Î”f(m.grid, ihâ‚) * (m.parameters.CRi - Riâ‚) / Î”Ri
    end

    -zâ˜… < 0 && @warn "mixing depth $(-zâ˜…) is negative"

    return -zâ˜… # "depth" is negative height.
end

#
# Vertical velocity scale
#

"Return true if the boundary layer is unstable and convecting."
@inline isunstable(model) = model.state.Fb > 0

"Return true if the boundary layer is forced."
@inline isforced(model) = model.state.Fu != 0 || model.state.Fv != 0 || model.state.Fb != 0

"Return the turbuent velocity scale associated with wind stress."
@inline Ï‰Ï„(Fu, Fv) = (Fu^2 + Fv^2)^(1/4)
@inline Ï‰Ï„(m::AbstractModel) = Ï‰Ï„(m.state.Fu, m.state.Fv)

"Return the turbuent velocity scale associated with convection."
@inline Ï‰b(Fb, h) = abs(h * Fb)^(1/3)
@inline Ï‰b(m::AbstractModel) = Ï‰b(m.state.Fb, m.state.h)

"Return the vertical velocity scale at depth d for a stable boundary layer."
@inline ğ’²_stable(CÏ„, Cstab, Cn, Ï‰Ï„, Ï‰b, d) = CÏ„ * Ï‰Ï„ / (1 + Cstab * d * (Ï‰b/Ï‰Ï„)^3)^Cn

"Return the vertical velocity scale at scaled depth dÏµ for an unstable boundary layer."
@inline function ğ’²_unstable(CSL, Cd, CÏ„, Cunst, Cb, CÏ„b, CmÏ„, Cmb, Ï‰Ï„, Ï‰b, d)
    dÏµ = min(CSL, d)
    if dÏµ * Ï‰b^3 < Cd * Ï‰Ï„^3
        return CÏ„ * Ï‰Ï„ * (1 + Cunst * dÏµ * (Ï‰b/Ï‰Ï„)^3)^CmÏ„
    else
        return Cb * Ï‰b * (dÏµ + CÏ„b * (Ï‰Ï„/Ï‰b)^3)^Cmb
    end
end

@propagate_inbounds function ğ’²_unstable_U(m, i)
    return ğ’²_unstable(m.parameters.CSL, m.parameters.Cd_U,
                            m.parameters.CÏ„, m.parameters.Cunst,
                            m.parameters.Cb_U, m.parameters.CÏ„b_U,
                            m.parameters.CmÏ„_U, m.parameters.Cmb_U,
                            Ï‰Ï„(m), Ï‰b(m), d(m, i)
                            )
end

@propagate_inbounds function ğ’²_unstable_T(m, i)
    return ğ’²_unstable(m.parameters.CSL, m.parameters.Cd_T,
                            m.parameters.CÏ„, m.parameters.Cunst,
                            m.parameters.Cb_T, m.parameters.CÏ„b_T,
                            m.parameters.CmÏ„_T, m.parameters.Cmb_T,
                            Ï‰Ï„(m), Ï‰b(m), d(m, i)
                            )
end

@propagate_inbounds function ğ’²_stable(m, i)
    return ğ’²_stable(m.parameters.CÏ„, m.parameters.Cstab, m.parameters.Cn,
                          Ï‰Ï„(m), Ï‰b(m), d(m, i)
                          )
end

"Return the turbulent velocity scale for momentum at face point i."
@propagate_inbounds function ğ’²_U(m::AbstractModel{TS, G, T}, i) where {TS, G, T}
    if !isforced(m)
        return -zero(T)
    elseif isunstable(m)
        return ğ’²_unstable_U(m, i)
    else
        return ğ’²_stable(m, i)
    end
end

"Return the turbulent velocity scale for tracers at face point i."
@propagate_inbounds function ğ’²_T(m::AbstractModel{TS, G, T}, i) where {TS, G, T}
    if !isforced(m)
        return -zero(T)
    elseif isunstable(m)
        return ğ’²_unstable_T(m, i)
    else
        return ğ’²_stable(m, i)
    end
end

const ğ’²_V = ğ’²_U
const ğ’²_S = ğ’²_T

## ** The K-Profile-Parameterization **
K_KPP(h, ğ’², d, shape=default_K_shape) = ifelse(0<d<1, max(zero(h), h*ğ’²*shape(d)), -zero(h))

#
# Non-local flux
#

"""
    NL(CNL, flux, d, shape=default_shape)

Returns the nonlocal flux, N = CNL*flux*shape(d),
where `flux` is the flux of some quantity out of the surface,
`shape` is a shape function, and `d` is a non-dimensional depth coordinate
that increases from 0 at the surface to 1 at the bottom of the mixing layer.

Because flux is defined as pointing in the positive direction,
a positive surface flux implies negative surface flux divergence,
which implies a reduction to the quantity in question.
For example, positive heat flux out of the surface implies cooling.
"""
@inline NL(CNL, flux, d, shape=default_NL_shape) = CNL * flux * shape(d)

@inline function âˆ‚NLâˆ‚z(CNL::T, FÏ•, dáµ¢â‚Šâ‚, dáµ¢, Î”f, m) where T
    if isunstable(m)
        return (NL(CNL, FÏ•, dáµ¢â‚Šâ‚) - NL(CNL, FÏ•, dáµ¢)) / Î”f
    else
        return -zero(T)
    end
end

@propagate_inbounds âˆ‚NLTâˆ‚z(m, i) =
    âˆ‚NLâˆ‚z(m.parameters.CNL, m.state.FÎ¸, d(m, i+1), d(m, i), Î”f(m.grid, i), m)

@propagate_inbounds âˆ‚NLSâˆ‚z(m, i) =
    âˆ‚NLâˆ‚z(m.parameters.CNL, m.state.Fs, d(m, i+1), d(m, i), Î”f(m.grid, i), m)

#
# Equation specification
#

# K_{U,V,T,S} is calculated at face points
@propagate_inbounds KU(m, i) = K_KPP(m.state.h, ğ’²_U(m, i), d(m, i)) + m.parameters.KUâ‚€
@propagate_inbounds KT(m, i) = K_KPP(m.state.h, ğ’²_T(m, i), d(m, i)) + m.parameters.KTâ‚€
@propagate_inbounds KS(m, i) = K_KPP(m.state.h, ğ’²_S(m, i), d(m, i)) + m.parameters.KSâ‚€
const KV = KU

@propagate_inbounds RU(m, i) =   m.constants.f * m.solution.V[i]
@propagate_inbounds RV(m, i) = - m.constants.f * m.solution.U[i]
@propagate_inbounds RT(m, i) = - âˆ‚NLTâˆ‚z(m, i)
@propagate_inbounds RS(m, i) = - âˆ‚NLSâˆ‚z(m, i)

#####
##### Some utilities
#####

function nonlocal_salinity_flux!(flux, m)
    for i in interiorindices(flux)
        @inbounds flux[i] = NL(m.parameters.CNL, m.state.Fs, d(m, i))
    end
    return nothing
end

function nonlocal_temperature_flux!(flux, m)
    for i in interiorindices(flux)
        @inbounds flux[i] = NL(m.parameters.CNL, m.state.FÎ¸, d(m, i))
    end
    return nothing
end

function nonlocal_salinity_flux(model)
    flux = FaceField(model.grid)
    nonlocal_salinity_flux!(flux, model)
    return flux
end

function nonlocal_temperature_flux(model)
    flux = FaceField(model.grid)
    nonlocal_temperature_flux!(flux, model)
    return flux
end

end # module
